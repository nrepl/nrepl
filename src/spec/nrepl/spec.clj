(ns nrepl.spec
  "This serves as a reference for messages as implemented by the default nREPL
  middleware. This is not a formal specification."
  (:require [clojure.spec.alpha :as s]
            [clojure.string :as str]))

;; Common

;; Op is a string
;; `session` is only generated by nREPL itself, and thus will be a UUID.
;; `id` is provided by the client, and is any string
;; `status` is the return status for a message

(s/def ::op string?)

(s/def ::uuid-str #(and (string? %)
                        (re-matches #"[a-f0-9]{8}-[a-f0-9]{4}-[a-f0-9]{4}-[a-f0-9]{4}-[a-f0-9]{12}"
                                    (str/lower-case %))))

(s/def ::session (s/or :single ::uuid-str
                       :multi  (s/coll-of ::uuid-str :kind? set?)))

(s/def ::id string?)

(s/def ::status (s/or :single keyword?
                      :multi  (s/coll-of keyword? :kind? set?)))

;; Parameters for Clone

;; `session` is optionally provided.
;; `client-name`, is optionally provided
;; `client-version`, is optionally provided
;; `new-session`, an UUID, is returned

(s/def ::new-session ::uuid-str)

(s/def ::client-name string?)

(s/def ::client-version string?)

;; Parameters for Describe

;; `verbose` is optionally provided

;; `versions` for Java, Clojure, and nREPL are returned.
;; `ops` for available ops is also returned.
;; `aux` is auxiliary data contributed by middleware via :describe-fn
;; `middleware` is the list of active middleware as strings
;; `current-ns` is provided via the session middleware's describe-fn

(s/def ::verbose? boolean?)

(s/def ::doc string?)

(s/def ::requires (s/map-of keyword? string?))

(s/def ::optional (s/map-of keyword? string?))

(s/def ::returns (s/map-of keyword? string?))

(s/def ::description (s/keys :opt-un [::doc ::requires ::optional ::returns]))

(s/def ::ops (s/map-of keyword? ::description))

(s/def ::major (s/or :integer int?
                     :string  string?))

(s/def ::minor (s/or :integer int?
                     :string  string?))

(s/def ::incremental (s/or :integer int?
                           :string  string?))

(s/def ::version-string string?)

(s/def ::version (s/keys :opt-un [::major ::minor ::incremental ::version-string]))

(s/def ::versions (s/map-of keyword? ::version))

(s/def ::aux map?)

(s/def ::middleware (s/coll-of string?))

(s/def ::current-ns string?)

;; Parameters for Eval

;; `code` is provided as a string.
;; `line`, `column` and `file` are provided for location in source.
;; `read-cond` controls the reader's conditional behavior (:allow or :preserve).
;; `eval` specifies an alternative evaluation function.

;; `value` holds the return value
;; `out` holds what was printed to stdout
;; `err` holds what was printed to stderr
;; `ns` holds the value of `*ns*` after the eval.

(s/def ::code string?)

(s/def ::read-cond keyword?)

(s/def ::eval string?)

(s/def ::out string?)

(s/def ::err string?)

(s/def ::value any?)

(s/def ::line int?)

(s/def ::column int?)

(s/def ::file string?)

(s/def ::ex string?)

(s/def ::root-ex string?)

(s/def ::ns string?)

;; Parameters for Print middleware
;;
;; These are optional parameters that can be included in eval and load-file
;; requests to control how values are printed.

(s/def :nrepl.middleware.print/print symbol?)

(s/def :nrepl.middleware.print/options (s/nilable map?))

(s/def :nrepl.middleware.print/stream? any?) ;; logical true/false

(s/def :nrepl.middleware.print/buffer-size pos-int?)

(s/def :nrepl.middleware.print/quota pos-int?)

(s/def :nrepl.middleware.print/keys (s/coll-of keyword?))

;; Parameters for Caught middleware
;;
;; These are optional parameters that can be included in eval and load-file
;; requests to control how errors are conveyed.

(s/def :nrepl.middleware.caught/caught symbol?)

(s/def :nrepl.middleware.caught/print? any?) ;; logical true/false

;; Parameters for Load File

;; `file-name` and `file-path` are required.
;; Return params are similar to `eval`

(s/def ::file-name string?)

(s/def ::file-path string?)

;; Parameters for List Sessions

;; `sessions`: Returns sessions in a set

(s/def ::sessions (s/coll-of ::uuid-str :kind? set?))

;; Parameters for Std In

;; `stdin`: string value to be piped into std-in

(s/def ::stdin string?)

;; Parameters for Interrupt

;; `interrupt-id` optionally specifies which request to interrupt

(s/def ::interrupt-id string?)

;; Parameters for Completion

;; `prefix` is the string to complete.
;; `complete-fn` is an optional fully-qualified function name.
;; `options` is an optional map of options for the completion/lookup function.
;; `completions` is the list of completion candidates returned.

(s/def ::prefix string?)

(s/def ::complete-fn string?)

(s/def ::options map?)

(s/def ::completions (s/coll-of map?))

;; Parameters for Lookup

;; `sym` is the symbol to look up.
;; `lookup-fn` is an optional fully-qualified function name.
;; `info` is the map of symbol info returned.

(s/def ::sym string?)

(s/def ::lookup-fn string?)

(s/def ::info map?)

;; Message map def

(s/def ::message (s/keys :opt-un [::session ::id ::status ::client-name ::client-version
                                  ::new-session
                                  ::op ::code ::out ::err ::value ::file ::line ::column
                                  ::read-cond ::eval
                                  ::ex ::root-ex ::ns
                                  ::file-name ::file-path
                                  ::ops ::verbose? ::versions ::aux ::middleware
                                  ::current-ns
                                  ::sessions
                                  ::stdin
                                  ::interrupt-id
                                  ::prefix ::complete-fn ::options ::completions
                                  ::sym ::lookup-fn ::info]
                         :opt [:nrepl.middleware.print/print
                               :nrepl.middleware.print/options
                               :nrepl.middleware.print/stream?
                               :nrepl.middleware.print/buffer-size
                               :nrepl.middleware.print/quota
                               :nrepl.middleware.print/keys
                               :nrepl.middleware.caught/caught
                               :nrepl.middleware.caught/print?]))
